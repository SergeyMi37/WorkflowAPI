<?xml version="1.0" encoding="UTF-8"?>
<Export generator="IRIS" version="26">
<Class name="isc.wf.REST">
<Super>%CSP.REST</Super>
<TimeCreated>65608,61785.443096</TimeCreated>

<Parameter name="CONTENTTYPE">
<Default>application/json</Default>
</Parameter>

<Parameter name="CHARSET">
<Default>UTF-8</Default>
</Parameter>

<Parameter name="UseSession">
<Type>Integer</Type>
<Default>1</Default>
</Parameter>

<XData name="UrlMap">
<XMLNamespace>http://www.intersystems.com/urlmap</XMLNamespace>
<Data><![CDATA[
<Routes>
<Route Url="/tasks" Method="GET" Call="getTasks"/>
<Route Url="/tasks/:count" Method="GET" Call="getTasks"/>
<Route Url="/tasks/:count/:maxId" Method="GET" Call="getTasks"/>

<Route Url="/task/:id" Method="GET" Call="getTask"/>
<Route Url="/task/:id" Method="POST" Call="postTask"/>

<Route Url="/test" Method="GET" Call="test"/>
<Route Url="/logout" Method="GET" Call="logout"/>
<Route Url="/user-info" Method="GET" Call="getUserInfo"/>
</Routes>
]]></Data>
</XData>

<Method name="logout">
<Description>
End session</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim %session As %CSP.Session
	set sc = %session.Logout(1)
	set %session.EndSession = 1
	quit sc
]]></Implementation>
</Method>

<Method name="test">
<Description>
Test method</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    write "{""Status"": ""OK""}"
    quit $$$OK
]]></Implementation>
</Method>

<Method name="getTasks">
<Description>
Get tasks. Supports pagination.
count - number of records to get
maxId - latest id, leave empty to start from the begining
set sc = ##class(isc.wf.REST).getTasks()</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>count=100,maxId=""</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	#dim isFirst As %Boolean = $$$YES
	&sql(	DECLARE C1 CURSOR FOR
			SELECT TOP :count 
				 %ID,  IsNew,  Task->%Priority, Task->%Subject, Left(Task->%Message,25), TimeCreated,  RoleName
			INTO :id, :isNew, :priority,        :subject,       :message,                :timeCreated, :role
			FROM EnsLib_Workflow.Worklist
			WHERE (UserName = USER) 
				AND ((Task->TaskStatus_AssignedTo IS NULL) OR (Task->TaskStatus_AssignedTo = UserName))
				AND ((%ID < :maxId) OR (:maxId IS NULL))
			ORDER BY %ID DESC)
	&sql(OPEN C1)
	&sql(FETCH C1)
	write:((SQLCODE=0) || (SQLCODE=100)) "["
	while (SQLCODE = 0) {
		if isFirst {
			set isFirst = $$$NO
		} else {
			write ",",!
		}
		write "{""id"":""", id,""",",
		 			"""isNew"":",isNew,",",
		 			"""priority"":",priority,",",
		 			"""subject"":""", $zcvt(subject,"O", "JSON"),""",",
		 			"""message"":""", $zcvt(message,"O", "JSON"),""",",
		 			"""timeCreated"":""", timeCreated,""",",
		 			"""role"":""", role,"""}"
		&sql(FETCH C1)
	}
	if (SQLCODE'=100) {
		set sc = $$$ERROR($$$SQLError, SQLCODE, $g(%msg))
	} else {
		write "]"
	}
	&sql(CLOSE C1)
	
	quit sc
]]></Implementation>
</Method>

<Method name="getTask">
<Description>
Get one task
set sc = ##class(isc.wf.REST).getTask("318||dev")</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>id</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	&sql(	SELECT %ID,  IsNew,  Task->%Priority, Task->%Subject, Task->%Message, TimeCreated,  RoleName,  Task,  Task->%Actions, Task->%FormFields
			INTO   :id, :isNew, :priority,        :subject,       :message,       :timeCreated, :role,     :task, :actions,       :formFields
			FROM EnsLib_Workflow.Worklist
			WHERE (%ID = :id) AND (UserName = USER))
	if (SQLCODE = 0) {
		write "{""id"":""", id,""",",
		 			"""isNew"":",isNew,",",
		 			"""priority"":",priority,",",
		 			"""subject"":""",$zcvt(subject,"O", "JSON"),""",",
		 			"""message"":""",$zcvt(message,"O", "JSON"),""",",
		 			"""timeCreated"":""",timeCreated,""",",
		 			"""role"":""",role,""",",
		 			"""actions"":""",actions,""",",
		 			"""formFields"":{"
		 
		merge fieldValues = ^Ens.MessageBodyD(task, "%FormValues")	
		set fieldCount = $case(formFields, "":0, :$l(formFields, ","))
		for field = 1:1:fieldCount {
			set fieldName = $p(formFields, ",", field)
			continue:fieldName=""
			set fieldValue = $g(fieldValues(fieldName))
			
			// Special handlers
			// "$$$Handler": {"myLink":"link","myImg":"img"}
			if fieldName = "$$$Handler" {
				write """",$zcvt(fieldName,"O", "JSON"),""":",fieldValue
			} else {
				write """",$zcvt(fieldName,"O", "JSON"),""":""",$zcvt(fieldValue,"O", "JSON"),""""
			}
			write:(field<fieldCount) ","
		 }
		 write "}}"
	} else {
		set sc = $$$ERROR($$$SQLError, SQLCODE, $g(%msg))
	}
	
	quit sc
]]></Implementation>
</Method>

<Method name="postTask">
<Description>
Submit task. Works only in CSP context
formFields - pass all field values as json object properties
{ "action": "action1", "formFields": {}}</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>id:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim %request As %CSP.Request
	// For EnsLib.Workflow.FormEngine compatibility
	set %request.Data("$ITEMID",1) = id
	
	set taskResponse = ##class(EnsLib.Workflow.TaskResponse).%OpenId($p(id, "||", 1), , .sc)
	quit:$$$ISERR(sc) sc
	
	set owner = taskResponse.%TaskStatus.AssignedTo
	quit:((owner'=$username) && (owner '="")) $$$ERROR($$$GeneralError, "User " _ $username _ " does not have rights to modify this task")
	
	set %request.Content = {}.%FromJSON(%request.Content)
	set taskResponse.%Action = %request.Content.action
	
	set iterator = %request.Content.formFields.%GetIterator()
	while iterator.%GetNext(.key, .value) {
		set %request.Data(key,1) = value
	}
	
	quit ##class(EnsLib.Workflow.FormEngine).%OnSubmit(taskResponse, .msq)
]]></Implementation>
</Method>

<Method name="OnPreDispatch">
<Description>
Only Workflow users can use this api</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[pUrl:%String,pMethod:%String,&pContinue:%Boolean]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set pContinue = ##class(EnsLib.Workflow.UserDefinition).%ExistsId($username)
    quit $$$OK
]]></Implementation>
</Method>

<Method name="Login">
<Description>
Called for a REST page in the event of a login being required</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>skipheader:%Boolean=1</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim tSC As %Status = $$$OK
    #dim e As %Exception.AbstractException
    
    #dim tMatchURL As %String
			$$$SysLog(3,"OAuth2","[%CSP.REST]","Login: %request.Method="_%request.Method)
           
    Try {
        
        #; Don't want the session token
        Set %response.OutputSessionToken=0
        
        #; Also set language to prevent any attempt to call %response.MatchLanguage as this can cause PROTECT errors reading from ^IRIS.Msg global
        If %response.Language="" Set %response.Language="en"
        
        Set tMatchURL=$Extract(%request.URL,$Length(%request.Application),*)
      
        #; Process CORS request
        Set tSC=..ProcessCorsRequest(tMatchURL)
        If $$$ISERR(tSC) Quit
            
        If %request.Method="OPTIONS" {
            
            #; We want to allow OPTIONS requests even if not authorized
            Set tSC=..OnHandleOptionsRequest(tMatchURL)
            If $$$ISERR(tSC) Quit
         
        } else {
            
            #; Set the Http Status
            Set %response.Status=..#HTTP401UNAUTHORIZED
            
            #; We do not want Basic authentication
            #; Do %response.SetHeader("WWW-Authenticate","Basic")
        }
        
    } Catch (e) {
        Set tSC=e.AsStatus()
    }
    
    #; We need to manually write out the headers here because PAGE has not been called
    Do %response.WriteHTTPHeader()
        
    #; Done
    Quit tSC
]]></Implementation>
</Method>

<Method name="outputStatus">
<Description>
This method takes a status, renders it as json (if requested) and outputs the result</Description>
<Internal>1</Internal>
<ClassMethod>1</ClassMethod>
<FormalSpec>pSC:%Status</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim tSC As %Status = $$$OK
    #dim e As %Exception.AbstractException
    
    try {
        
        #dim tJSON As %ZEN.proxyObject
    
        if ..AcceptsContentType("application/json") {
        
            set %response.ContentType = ..#CONTENTTYPEJSON
        
            #; Convert the exception to a status and render to JSON
            set tSC = ..StatusToProxyObject(pSC, .tJSON)
            set tJSON.stack = ..getDebugInfo()
            if $$$ISERR(tSC) Quit

            #; Write the JSON to the output device
            set tSC = tJSON.%ToJSON(, "aeloqutwc")
            if $$$ISERR(tSC) Quit
            
        } else {
            
            #; Set plain text
            set %response.ContentType = ..#CONTENTTYPETEXT
            
            #; Write out a simple text message
            do ##class(%Exception.StatusException).CreateFromStatus(pSC).OutputToDevice()
        }
        
    } catch ex {
        set tSC = ex.AsStatus()
    }
    quit $$$OK
]]></Implementation>
</Method>

<Method name="getDebugInfo">
<ClassMethod>1</ClassMethod>
<ReturnType>%ZEN.proxyObject</ReturnType>
<Implementation><![CDATA[
	set obj = ##class(%ZEN.proxyObject).%New()
	set obj.stack = ..getStackInfo()
	set obj.objlasterror = $system.Status.GetErrorText($get(%objlasterror))
	set obj.request = %request
	set obj.response = %response
	set obj.user = $username
	
	quit obj
]]></Implementation>
</Method>

<Method name="getStackInfo">
<ClassMethod>1</ClassMethod>
<ReturnType>%ListOfDataTypes</ReturnType>
<Implementation><![CDATA[

	set ex = ##class(%Exception.SystemException).%New()
	do ex.StackAsArray(.stack)
	set list = ##class(%ListOfObjects).%New()
	for i=1:1:stack {
		set obj = ##class(%ZEN.proxyObject).%New()
		set obj.line = stack(i,"PLACE")
		set obj.call =  stack(i)
		set obj.part = $piece($piece(stack(i,"PLACE"), "^", *), " ", 1)
		do list.Insert(obj)
	}
	
	quit list
]]></Implementation>
</Method>

<Method name="getUserInfo">
<Description>
Get user info</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim %session As %CSP.Session
	set user = {
		"username": ($Username),
		"timeout": (%session.AppTimeout)
	}
	write user.%ToJSON()
	quit $$$OK
]]></Implementation>
</Method>
</Class>
</Export>
